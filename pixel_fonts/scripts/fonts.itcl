# vim: foldmarker=<<<,>>>

class pixel::fonts::Font {
	constructor {args} {}

	public {
		method add_font_spec {specfile}
		method add_font {basename attribs filename}

		method get_face {fontdesc}
		method list_basenames {}
		method list_attribs {basename}
		method add_ttf_files {files}
	}

	private {
		variable reg
		variable attribmap
		variable allowed_attribs	{normal bold italic mono}

		method compile_fontname {basename attribs}
	}
}


body pixel::fonts::Font::constructor {args} { #<<<1
	array set reg	{}
	set attribmap(normal)	0
	set exp					0
	foreach attrib {
		normal bold italic mono
	} {
		set attribmap($attrib)	[expr {1 << $exp}]
		incr exp
	}

	add_font_spec [file join $::pixel::fonts::library fonts fonts.spec]
	
	eval configure $args
}


body pixel::fonts::Font::get_face {fontdesc} { #<<<1
	set basename	[lindex $fontdesc 0]
	set attribs		[lindex $fontdesc 1]
	set name	[compile_fontname $basename $attribs]
	if {![info exists reg($name)]} {
		error "No such font: $basename, $attribs"
	}

	# Force face compilation on our copy, so it's only done once per font
	pixel::ttf::compile_face $reg($name)

	return $reg($name)
}


body pixel::fonts::Font::compile_fontname {basename attribs} { #<<<1
	set name	[string tolower [string trim $basename]]
	append name	,
	set aid		0
	foreach attrib $attribs {
		set attrib	[string tolower [string trim $attrib]]
		if {[info exists attribmap($attrib)]} {
			incr aid	$attribmap($attrib)
		} else {
			error "No such font attribute: ($attrib)"
		}
	}
	append name	$aid

	return $name
}


body pixel::fonts::Font::add_font_spec {specfile} { #<<<1
	if {![file exists $specfile]} {
		error "Specfile doesn't exist: ($specfile)"
	}

	set fp	[open $specfile r]
	set dat	[read $fp]
	close $fp

	foreach {basename spec} $dat {
		catch {unset tmp}
		array set tmp {
			attribs			{}
		}
		array set tmp $spec
		if {![info exists tmp(filename)]} {
			error "font $basename in $specfile doesn't give valid filename"
		}
		set fn	[file join [file dirname $specfile] $tmp(filename)]
		if {![file exists $fn]} {
			error "font $basename in $specfile doesn't give valid filename"
		}
		set attribs		$tmp(attribs)
		set filename	$fn

		add_font $basename $attribs $filename
	}
}


body pixel::fonts::Font::add_font {basename attribs filename} { #<<<1
	#puts "add_font: ($basename) ($attribs) ($filename)"
	set name		[compile_fontname $basename $attribs]
	set hold		[pwd]
	cd [file dirname $basename]
	set reg($name)	[list [file join [pwd] $filename]]
	cd $hold
	#puts "\t$name - $reg($name)"
}


body pixel::fonts::Font::list_basenames {} { #<<<1
	set build	{}
	foreach name [array names reg] {
		#puts "list_basenames: processing ($name)"
		lappend build	[lindex [split $name ,] 0]
	}

	return [lsort -dictionary -unique $build]
}


body pixel::fonts::Font::list_attribs {basename} { #<<<1
	set build	{}
	foreach name [array names reg "$basename,*"] {
		set tmp		[lindex [split $name ,] 1]
		set thisrow	{}
		foreach {attrib aid} [array get attribmap] {
			if {$tmp & $aid} {
				lappend thisrow	$attrib
			}
		}
		lappend build	$thisrow
	}
	return $build
}


body pixel::fonts::Font::add_ttf_files {files} { #<<<1
	foreach file $files {
		if {![file exists $file]} {
			error "File ($file) doesn't exist"
		}
		array set altinf	{}
		if {[catch {
			set inf	[pixel::ttf_info [list $file]]
		} msg]} {
			error "Couldn't get font info from ($file): $msg"
		}
		foreach item $inf {
			array unset fields
			array set fields $item
			if {$fields(platform) == "TT_PLATFORM_MACINTOSH" && [string trim $fields(string)] != ""} {
				set altinf($fields(type))	$fields(string)
				if {$fields(type) == "TT_NAME_ID_FONT_SUBFAMILY"} {
					if {![info exists used($fields(string))]} {
						set used($fields(string))	1
					} else {
						incr used($fields(string))
					}
				}
			}
		}

		if {![info exists altinf(TT_NAME_ID_FONT_SUBFAMILY)]} {
			error "Can't get subfamily info from ($file)"
		}
		if {![info exists altinf(TT_NAME_ID_FONT_FAMILY)]} {
			error "Can't get family info from ($file)"
		}
		set raw		[string tolower $altinf(TT_NAME_ID_FONT_SUBFAMILY)]
		set attribs	{}
		foreach attrib $raw {
			switch -- $attrib {
				"regular" -
				"normal" {
					continue
				}

				"oblique" {
					lappend attribs	"italic"
				}

				default {
					if {[lsearch $allowed_attribs $attrib] == -1} {
						continue
					}
					lappend attribs	$attrib
				}
			}
		}
		#puts "$file - {[list [string tolower $altinf(TT_NAME_ID_FONT_FAMILY)] $attribs]}"
		add_font $altinf(TT_NAME_ID_FONT_FAMILY) $attribs $file
	}
}


